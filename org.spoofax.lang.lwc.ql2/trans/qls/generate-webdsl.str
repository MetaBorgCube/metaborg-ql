module qls/generate-webdsl

imports
	
	libstratego-lib
	libstratego-gpp
	libstratego-aterm
	lib/editor-common.generated

	include/QL
	lib/webdsl/WebDSL
	lib/runtime/nabl/-
	lib/runtime/index/-

  ql/generation/generate-webdsl
	ql/store
	
rules // QLS to WebDSL compilation
	
	compile-qls-webdsl: 
		QLS(_, page*) ->
		|[
			module form
			
			def1*
			
			ent
			
			session sess { user -> User	}
			
			def2*
			
		  page root() {
				var name := ""
				
				main() {
					horizontalForm() {
						controlGroup("Name: ") { input(name) }
						
				    formActions() {
				    	submit start() [style="display: none;"] {}
				      submitlink start() [class="btn btn-primary"] { "Start questionaire" } " "
				    }
					}
				}
				
			  action start() {
			    x_init;
			    return page1();
			  }
			}
			
			page finish() {
				main() {
					"Thank you for filling in the form"
				}
			}
		]|
		with
			fref*  := <mapconcat(qls-page-forms); make-set> page*;
			def1*  := <map(compile-qls-webdsl-entity)> fref*;
			ent    := <compile-qls-webdsl-session-entity> fref*;
			x_init := <compile-qls-webdsl-session-init> fref*;
			def2*  := <zip(compile-qls-webdsl-page(|<length> page*))> (page*, <length; upto; Tl> page*)

rules // QLS pages to WebDSL entities
	  	
  compile-qls-webdsl-entity:
  	FormRef(x_formID) -> |[ entity x_formID { ebd* } ]|
  	where
	  	ebd* := <qls-get-question-asts-in-form; collect-all(compile-ql-webdsl-entity-part)> x_formID
	  	
rules // QLS pages to WebDSL session Entity
	
	compile-qls-webdsl-session-entity:
		fref* -> 
		|[
			entity User {
				name :: String (name)
					
			  ebd*
			}
		]|
		with
			ebd* := <map(compile-qls-webdsl-session-entity-part)> fref*
	
	compile-qls-webdsl-session-entity-part:
		FormRef(x_formID) -> |[ x_formID -> x_formID ]|
		
	compile-qls-webdsl-session-init:
		fref* -> |[ sess.user := User { name := name, x_inits } ]|
		with
			x_inits := <map(compile-qls-webdsl-session-init-part)> fref*
			
	compile-qls-webdsl-session-init-part:
		FormRef(x_formID) -> |[ x_formID := x_formID {} ]|
	  	
rules // QLS page to WebDSL page	
	
	compile-qls-webdsl-page(|page_length):
		(Page(x_heading, fref*, part*), n) -> // TODO: Concrete syntax match? meta variable for STRING does not work..
		def |[
	    page x_pageName() {
    		elem_var*
    		
    		init {
    			stat_init*
    		}
	    		
	    	main() {	    		
		    	horizontalForm(x_heading) {
		  		  elem_group*
		  		  
				    formActions() {
				      submitlink submit() [class="btn btn-primary"] { "Submit" } " "
				      elem_nextSubmit
				    }
				  }
		  	}
				  
			  action submit() {
			  	stat_action*
			  	stat_saveall*
			  }
			  elem_nextAction
			  
		    action ignore-validation update(phs:[String]){
		      for(ph in phs){
		        replace(ph);
		      }
		      rollback();
		    }
	  	}
		]|
	  with
	  	x_pageName     := <conc-strings> ("page", <int-to-string> n);
	  	stat_saveall*  := <map(compile-qls-webdsl-page-save)> fref*;
	  	qref_part*     := <collect-all(?StyledRef(_, _) <+ ?SimpleRef(_))> part*;
	  	parts_length   := <length> qref_part*;
	  	{| Var, Init, Action:
	  		counter      := <new-counter>;
	  		elem_group*  := <map(compile-qls-webdsl-part(|parts_length, counter)); concat> part*;
		  	elem_var*    := <bagof-Var <+ ![]>;
		  	stat_init*   := <bagof-Init <+ ![]>;
		  	stat_action* := <bagof-Action <+ ![]>
  		|};
	  	if <eq> (n, page_length) then
	  		elem_nextSubmit := elem* |[ submitlink finish() [class="btn btn-primary"] { "Finish" } " " ]|;
	  		elem_nextAction := elem  |[ action finish() { stat_action* stat_saveall* return finish(); } ]|
	  	else
	  		elem_nextSubmit := elem* |[ submitlink next() [class="btn btn-primary"] { "Next" } " " ]|;
	  		x_nextPageName  := <conc-strings> ("page", <int-to-string> <inc> n);
	  		elem_nextAction := elem  |[ action next() { stat_action* stat_saveall* return x_nextPageName(); } ]|
	  	end
	  	
  compile-qls-webdsl-page-save:
  	FormRef(fref) -> |[ x_formID.save(); ]|
  	with
  		x_formID := <compile-ql-webdsl-form-name> fref

rules // QLS part to WebDSL elements
	  	
	compile-qls-webdsl-part(|length, counter):
		|[ qid { s* } ]| -> elem* |[placeholder x_p { x_q }]|
		with
			nr   := <next-counter> counter;
			body := <get-stored-ast> qid;
			x_p  := <compile-placeholder-name> nr;
			x_q  := <compile-qls-webdsl-group(|s*, length, nr)> body

	compile-qls-webdsl-part(|length, counter):
		SimpleRef(qid) -> elem* |[placeholder x_p { x_q }]|
		with
			nr   := <next-counter> counter;
			body := <get-stored-ast> qid;
			x_p  := <compile-placeholder-name> nr;
			x_q  := <compile-qls-webdsl-group(|[], length, nr)> body
			
	compile-qls-webdsl-part(|length, counter):
	 Section(elem_heading, part*) -> elem* |[ pageHeader() { elem_heading } elem_group* ]| // TODO: Concrete syntax match? meta variable for STRING does not work..
		with
			elem_group* := <map(compile-qls-webdsl-part(|length, counter))> part*
			
	compile-qls-webdsl-part(|length, counter):
		SubSection(elem_heading, part*) -> elem* |[ pageHeader2() { elem_heading } elem_group* ]| // TODO: Concrete syntax match? meta variable for STRING does not work..
		with
			elem_group* := <map(compile-qls-webdsl-part(|length, counter))> part*
			
rules // QL group to WebDSL page elements
	
	// Regular question
  compile-qls-webdsl-group(|styles, part_length, nr): 
  	q@|[ qid : label t ]| -> elem |[ controlGroup(e_label) [passign_style*] { elem_input } ]|
  	with
  		e_label        := <compile-ql-webdsl-exp> label;
  		passign_style* := <compile-qls-webdsl-simple-styles> styles;
  		widget         := <fetch-elem(?TypedWidget(_, _)) <+ !()> styles;
  		elem_input     := <compile-qls-webdsl-group-input(|passign_style*, part_length, nr)> (widget, q)
  		
	// Computed question
  compile-qls-webdsl-group(|styles, part_length, nr): 
  	|[ qid : label t ( exp ) ]| -> elem |[ controlGroup(e_label) [passign_style*] { output(e_typeExp) [passign_style*] } ]|
  	with
  		e_label        := <compile-ql-webdsl-exp> label;
  		e_typeExp      := <compile-ql-webdsl-exp> exp;
  		passign_style* := <compile-qls-webdsl-simple-styles> styles
  		
	// Condition
  compile-qls-webdsl-group(|styles, part_length, nr): 
  	|[ if ( cond ) { q* } ]| -> elem |[ if (e_cond) { elem_group* } ]|
  	with
  		e_cond      := <compile-ql-webdsl-exp> cond;
  		elem_group* := <map(compile-qls-webdsl-group(|styles, part_length, nr))> q*

rules // QLS widget styles to WebDSL input
	
	compile-qls-webdsl-group-input(|passign_style*, part_length, nr) =
		compile-qls-webdsl-group-input-special(|passign_style*, part_length, nr)
		<+ compile-qls-webdsl-group-input-default(|passign_style*, part_length, nr)
	
	// Default style
	compile-qls-webdsl-group-input-default(|passign_styles, part_length, nr):
		(_, |[ qid : label t ]|) -> result
		with
			x_id         := <compile-ql-webdsl-question-name> qid;
			x_updates    := <compile-update-list(|part_length, nr)>;
			passign_all* := [passign |[oninput=update(x_updates)]|, passign_styles];
			switch !t
				case DateTy() : !call |[ input(x_id) [passign_all*] ]| // inputajax does not support dates
				otherwise     : !call |[ inputajax(x_id) [passign_all*] ]|
			end => result
	
	// Special styling for boolean widgets with radio or dropdown style.		
	compile-qls-webdsl-group-input-special(|passign_styles, part_length, nr):
		(TypedWidget(BoolTy(), widget), |[ qid : label t ]|) -> call |[ x_call [passign_all*] ]|
		where
			<?Radio() <+ ?Dropdown()> widget
		with
			x_oid        := <compile-ql-webdsl-question-name> qid;
			x_id         := <conc-strings> (qid, "_wrap");
			x_call       := <compile-qls-webdsl-group-input-call(|x_id)> widget;
			x_updates    := <compile-update-list(|part_length, nr)>;
			passign_all* := [passign |[onchange=update(x_updates)]|, passign_styles];
			rules(
				Var    :+ _ -> |[ var x_id : BoolWrap; ]|
				Init   :+ _ -> |[ if(x_oid) { x_id := bw_true; } else { x_id := bw_false; } ]|
				Action :+ _ -> |[ x_oid := x_id.value; ]|
			)
			
	compile-qls-webdsl-group-input-call(|x_id):
		Radio() -> call |[ radio(x_id) ]|
		
	compile-qls-webdsl-group-input-call(|x_id):
		Dropdown() -> call |[ select(x_id)[not null] ]|
			
rules // QLS simple styles to WebDSL property assignments
	
	compile-qls-webdsl-simple-styles:
		style* -> passign |[ style = x_style ]|
		with
			x_style := <filter(compile-qls-webdsl-simple-style); concat-strings; double-quote> style*
	
  compile-qls-webdsl-simple-style:
  	Font(name) -> $[font-family: '[<try(un-double-quote)> name]';]
  	
  compile-qls-webdsl-simple-style:
  	Color(color) -> $[color: [<try(un-double-quote)> color];]

rules // Projections
	
  qls-page-forms:
  	Page(_, fref*, _) -> fref* // TODO: Concrete syntax match? meta variable for STRING does not work..
	
rules // Index

	qls-get-question-asts-in-form:
		form-ref -> question-ast*
		with
			form-uri      := <nabl-uri> form-ref;
			question-def* := <nabl-get-child-definitions(|NablNsQuestion())> form-uri;
			question-ast* := <map(get-stored-ast)> question-def*
