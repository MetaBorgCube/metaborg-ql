module dependencies

imports
	index-library.generated
	analysis-library.generated
	include/QL
	names
	
signature constructors
	
	Label				: DefDataKind
	Condition		: DefDataKind
	Dependencies: DefDataKind
	
rules
	
	analyze-dependencies: 
		|[ form fid { question* } ]|-> <filter(analyze-dependencies)> question*
			
	analyze-dependencies: 
		|[ if (c) { qid : label type } ]|	-> <index-add-all> [ ldata, cdata, qdata ]
		where 
			uri   := <index-uri> qid ;
			ldata := DefData(uri, Label(), label) ;
			cdata := DefData(uri, Condition(), c) ; 
			qdata := DefData(uri, Dependencies(), <collect-questions> c) 
	
	analyze-dependencies: 
	|[ if (c) { qid : label texpr } ]|	-> <index-add-all> [ ldata, cdata, qdata ]
		where 
			uri   := <index-uri> qid ;
			ldata := DefData(uri, Label(), label) ;
			cdata := DefData(uri, Condition(), c) ; 
			qdata := DefData(uri, Dependencies(), <collect-questions> (c, texpr)) 
			
	collect-questions = collect-all(?Ref(<id>), union)
	
rules
	
	get-labels = 
		index-lookup;
		index-get-data-all(|Label());
		make-set
	
	get-conditions = 
		index-lookup;
		index-get-data-all(|Condition());
		make-set
			
	get-defined-types = 
		index-lookup;
		index-get-data-all(|Type());
		make-set

	get-dependencies =
		index-lookup;
		index-get-data-all(|Dependencies());
		unions
		
	get-all-dependencies = 
		(is-list <+ MkSingleton);
		transitive-closure(get-dependencies)
	
rules
	
	transitive-closure(s) = transitive-closure(s|[])
	
	transitive-closure(s|accu): []		-> accu
	transitive-closure(s|accu): [h|t]	-> result
		where
			if <elem> (h, accu) then
				result := <transitive-closure(s|accu)> t
			else
				result := <conc; transitive-closure(s|[h|accu])> (<s> h, t)
			end
			
			
	index-add-all = index-add-all(|<index-get-current-file>)
	
	 