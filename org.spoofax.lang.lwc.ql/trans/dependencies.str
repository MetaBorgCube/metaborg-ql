module dependencies

imports
	index-library.generated
	analysis-library.generated
	include/QL
	names
	
signature constructors
	
	Dependencies: DefDataKind
	Label				: DefDataKind
	
rules
	
	analyze-dependencies: 
		|[ form fid { question* } ]|-> <mapconcat(analyze-dependencies; index-add-all)> question*
		
	analyze-dependencies: 
		|[ qid : label type ]|			->  [ DefData(uri, Dependencies(), [])
																		, DefData(uri, Label(), label)
													 					]
		where uri := <index-uri> qid
	
	analyze-dependencies: 
		|[ qid : label texpr ]|			->  [ DefData(uri, Dependencies(), <collect-questions> texpr)
																		, DefData(uri, Label(), label)
													 					]
		where uri := <index-uri> qid
	
	analyze-dependencies:
		|[ if (c) { question } ]|	-> <analyze-dependencies> question
		
	collect-questions = collect-all(?Ref(<id>), unions)
	
rules
	
	get-labels = 
		index-lookup;
		index-get-data-all(|Label());
		make-set
	
	get-conditions =
		index-lookup;
		index-get-data-all(|Dependencies());
		unions
		
	get-dependent-questions = 
		get-conditions;
		collect-all(?Ref(<id>), union)
		
	get-all-dependent-questions = 
		(is-list <+ MkSingleton);
		transitive-closure(get-dependent-questions)
	
	get-defined-types = 
		index-lookup;
		index-get-data-all(|Type());
		make-set

	
rules
	
	transitive-closure(s) = transitive-closure(s|[])
	
	transitive-closure(s|accu): []		-> accu
	transitive-closure(s|accu): [h|t]	-> result
		where
			if <elem> (h, accu) then
				result := <transitive-closure(s|accu)> t
			else
				result := <conc; transitive-closure(s|[h|accu])> (<s> h, t)
			end
			
			
	index-add-all = index-add-all(|<index-get-current-file>)
	
	 