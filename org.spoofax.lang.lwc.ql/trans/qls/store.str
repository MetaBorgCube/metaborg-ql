module qls/store

imports
	lib/index-library
	lib/analysis-library
	include/QL
	utils
	qls/types
	qls/defaults
	dependencies
	store
	names
	
signature
	constructors
		Dependencies: DefDataKind
		StyleDefinitions: DefDataKind
		ReferredQuestions: DefDataKind
		
signature
	constructors
		Page: Namespace
		
rules
	/*
	* Analyze and store dependencies of styles
	*/

	qls-analyze-dependencies:
		QLS(sdefs, _) -> <map(qls-analyze-dependencies)> sdefs
	
	qls-analyze-dependencies:
		|[ define style sid { stydef* } ]| -> <index-add-all> [ DefData(uri, Dependencies(), deps) ]
		with
			deps := <filter(?StyleRef(_))> stydef*;
			uri := <index-uri> sid

rules
	/*
	* Store styling attributes in index, easier for lookups
	*/
	
	qls-store-styles:
		QLS(sdefs, _) -> <map(qls-store-styles)> sdefs
		
	qls-store-styles:
		|[ define style sid { stydef* } ]| -> <index-add-all> [ DefData(uri, StyleDefinitions(), stydef*) ]
		with
			uri := <index-uri> sid
			
rules
	/*
	* Store a mapping for every form F to a list of all referred questions
	*		F -> [QuestionID]
	*/
	
	qls-store-pages = alltd(qls-store-question)
	
	qls-store-question = 
		(?SimpleRef(qid) + ?|[ qid { style* } ]|);
		!qid;
		qls-form-of-question;
		index-uri => frm-uri;
		(index-get-data(|ReferredQuestions()) <+ ![]) => data*;
		<index-remove> DefData(frm-uri, ReferredQuestions(), ());
		<index-add> DefData(frm-uri, ReferredQuestions(), [data*,qid])
		
	
	qls-form-of-question =
		index-lookup;
		index-uri;
		last;
		!<id>{[Frm(), <id>]};
		index-lookup			






















