module qls/styles

imports
	lib/index-library
	lib/analysis-library
	include/QL
	utils
	qls/types
	qls/defaults
	qls/store
	
rules
	/*
	* Query dependencies of style (SHALLOW)
	*/
	
	qls-get-dependent-styles =
		index-lookup;
		index-get-data-all(|Dependencies());
		unions;
		filter(?StyleRef(<id>))
	
	
	/*
	* Query dependencies of style (DEEP)
	*/
	qls-get-all-dependent-styles =
		(is-list <+ MkSingleton);
		transitive-closure(qls-get-dependent-styles)
	
	/*
	* Succeed if style has circular dependency (DEEP)
	*/
	qls-has-circular-dependency-style =
		?sid;
		qls-get-dependent-styles;
		qls-get-all-dependent-styles;
		fetch(?sid)

rules
	/*
	* Style linearization and concretization.
	* Basically recursively expand StyleRefs
	*/

	qls-make-concrete-style =
		?StyleRef(<id>);
		index-lookup;
		index-get-data(|StyleDefinitions())
	
	qls-make-all-concrete-style =
		map(try(not(?StyleRef(<id>);qls-has-circular-dependency-style);qls-make-concrete-style;qls-make-all-concrete-style));
		flatten-list
	
rules
	
	is-same-style =
		not(is-same-style-use);
		(
			?(Font(_),Font(_)) +
			?(Color(_), Color(_)) +
			?(Widget(_), Widget(_)) +
			?(TypedWidget(ty, _), TypedWidget(ty, _))
		)
	
	is-same-style-use = ?(StyleRef(a), StyleRef(a))

rules
	
	qls-normalize = qls-normalize-styles-top
	
	/* Normalize all SimpleRef and StyleRef.
	* - all StyleRefs are made concrete and simplified
	* - all SimpleRefs become StyleRef
	*/
	qls-normalize-styles-top = alltd(qls-normalize-styles)
	
	qls-normalize-styles:
		|[ qid { style0* }]| -> |[ qid { style1* }]|
		where
			qls-type-of => q-ty;
			qls-default-style(|q-ty) => def-style
		where
			<qls-make-all-concrete-style> style0*;
			<conc> (def-style, <id>);
			filter(is-typed-widget;untype-widget(|q-ty) <+ not(is-typed-widget));
			foldr(![], qls-add-style) => style1*			
	
	qls-normalize-styles:
		SimpleRef(qid) -> StyledRef(qid, Styles(<qls-default-style(|q-ty)>))
		where
			qls-type-of => q-ty


	is-typed-widget = ?TypedWidget(_, _)
	untype-widget(|ty): TypedWidget(ty, w) -> w 

	qls-add-style:
		(sty, styles) -> [sty|styles]
		where not(elem(is-same-style))
	
	qls-add-style:
		(sty, styles) -> styles
		where elem(is-same-style)

		
	
	