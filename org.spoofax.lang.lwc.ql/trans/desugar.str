module desugar

imports
	index-library.generated
	analysis-library.generated
	include/QL
	names
	
rules

	desugar-all:
		|[ form fid { q1* } ]| -> |[ form fid { q2* } ]|
		where
			<normalize-questions; innermost(normalize)> q1* => q2*

	normalize-questions: [] -> []
	normalize-questions =
		repeat(
				[normalize-question|id] 
			+ normalize-group
		); 
		[id|normalize-questions]
		
	normalize-question:
		|[ qid: label type ]| -> |[ if (true) { qid: label type } ]|
			
	normalize-question:
		|[ qid: label texpr ]| -> |[ if (true) { qid: label texpr } ]|
	
	normalize-question:
		|[ if (c1) { if (c2) { q* } } ]| -> |[ if (c1 && c2) { q* }]|
	
	normalize-group: 
		|[ if (c) { q1 q2 q1* } q2* ]| -> |[ if (c) { q1 } if(c) { q2 q1* } q2* ]|
		
			
rules
	
	// TODO: remove parentheses
	
	normalize: |[ exp1  <= exp2  ]| -> |[ (exp1 <  exp2) || (exp1 == exp2) ]|
	normalize: |[ exp1  >  exp2  ]| -> |[ exp2 <  exp1 ]|
	normalize: |[ exp1  >= exp2  ]| -> |[ exp2 <= exp1 ]|
	normalize: |[ exp   == exp   ]| -> |[ true ]|
	
	normalize: |[ true  && exp   ]| -> |[ exp   ]|
	normalize: |[ false && exp   ]| -> |[ false ]|
	normalize: |[ exp   && true  ]| -> |[ exp   ]|
	normalize: |[ exp   && false ]| -> |[ false ]|
	normalize: |[ true  || exp   ]| -> |[ true  ]|
	normalize: |[ false || exp   ]| -> |[ exp   ]|
	normalize: |[ exp   || true  ]| -> |[ true  ]|
	normalize: |[ exp   || false ]| -> |[ exp   ]|

	normalize: |[ !true           ]| -> |[ false ]|
	normalize: |[ !false          ]| -> |[ true  ]|
	normalize: |[ !!exp           ]| -> |[ exp   ]|
	normalize: |[ !(exp1 <  exp2) ]| -> |[ exp1  >= exp2  ]|
	normalize: |[ !(exp1 && exp2) ]| -> |[ (!exp1) || !exp2 ]|
	normalize: |[ !(exp1 || exp2) ]| -> |[ (!exp1) && !exp2 ]|
	
	normalize: |[ (exp1 && exp2) || exp3 ]| -> |[ (exp1 || exp3) && (exp2 || exp3) ]|
	normalize: |[ exp1 || (exp2 && exp3) ]| -> |[ (exp1 || exp2) && (exp1 || exp3) ]|
	normalize: |[ exp1 || (exp2 || exp3) ]| -> |[  exp1 || exp2  || exp3 ]|
	normalize: |[ exp1 && (exp2 && exp3) ]| -> |[  exp1 && exp2  && exp3 ]|
	
