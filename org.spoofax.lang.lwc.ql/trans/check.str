module check

imports
	libstratego-lib
	include/QL
	lib/editor-common.generated
	lib/index-library.generated
	lib/analysis-library.generated
	lib/analysis-library-internal.generated
	lib/nbl-library.generated

	types
	dependencies
	
rules // generic
	
	constraint-warning = fail
	
	constraint-note = fail
	
rules // expressions
	
	constraint-error =
		?|[ !exp ]|; 
		<type-error(eq)> (exp, BoolTy())
	
	constraint-error =
		( ?|[ exp1 && exp2 ]| + ?|[ exp1 || exp2 ]| );
		<filter(type-error(eq))> [(exp1, BoolTy()), (exp2, BoolTy())]
	
	constraint-error =
		(?|[ exp1 < exp2 ]| + ?|[ exp1 <= exp2 ]| + ?|[ exp1 > exp2 ]| + ?|[ exp1 >= exp2 ]|);
		<filter(type-error(is-comparable|"compared"))> [exp1, exp2];
		if ?[] then 
			<type-error(lub)> (exp2, <type-of> exp1)
		end
	
	constraint-error =
		?|[ exp1 == exp2 ]|;
		<type-error(lub)> (exp2, <type-of> exp1)
		
	constraint-error =
		(?|[ exp1 + exp2 ]| + ?|[ exp1 - exp2 ]|);
		<filter(type-error(is-addable|"added or subtracted"))> [exp1, exp2];
		if ?[] then 
			<type-error(lub)> (exp2, <type-of> exp1)
		end
		
	constraint-error =
		?|[ exp1 * exp2 ]|;
		if <type-of> exp1 => MoneyTy() then
			<type-error(lub)> (exp2, FloatTy())
		else if <type-of> exp2 => MoneyTy() then
			<type-error(lub)> (exp1, FloatTy())
		else
			<filter(type-error(is-numeric|"multiplied"))> [exp1, exp2];
			if ?[] then
				<type-error(lub)> (exp2, <type-of> exp1)
			end
		end end

	constraint-error =
		?|[ exp1 / exp2 ]|;
		if <type-of> exp1 => MoneyTy() then
			<type-error(lub)> (exp2, FloatTy())
		else
			<filter(type-error(is-numeric|"divided"))> [exp1, exp2];
			if ?[] then
				<type-error(lub)> (exp2, <type-of> exp1)
			end
		end

rules // questions and groups
	
	constraint-error: |[ type (exp) ]| 					 -> <type-error(lub)> (exp, type)
	constraint-error: |[ if (c) { question* } ]| -> <type-error(eq)> (c, BoolTy())

	constraint-warning =
		(?|[ qid : label type ]| + ?|[ qid : label texpr ]|);
		<get-labels> qid; 
		not(?[_]);
		!(label, $[Question has different labels.]) 
	
	constraint-error =
		(?|[ qid : label type ]| + ?|[ qid : label texpr ]|);
		<get-core-dependencies> qid;
		get-all-core-dependencies; fetch(?qid);
		!(qid, $[Question has cyclic dependency on itself.])
	
	constraint-warning =
		(?|[ qid : label type ]| + ?|[ qid : label texpr ]|);
		not(constraint-error);
		<get-dependencies> qid;
		get-all-dependencies; fetch(?qid);
		!(qid, $[Question has cyclic dependency on itself.])
	
	constraint-error =
		(?|[ qid : label type ]| + ?|[ qid : label texpr ]|);
		<get-defined-types> qid;
		not(?[_]);
		!(<!type <+ !texpr>, $[Question has incompatible types across multiple definitions])
	
rules // helpers
	
	type-error(s): 
		(e, expected) -> (e, $[Expression is of type [typexp1] which does not comply to [typexp2].])
		where 
			<type-of> e => actual; 
			<not(s)> (actual, expected);
			<pp-ql-string> actual => typexp1;
			<pp-ql-string> expected => typexp2
			
	type-error(s|quality): 
		e -> (e, $[Expression is of type [type] which cannot be [quality].])
		where 
			<type-of> e => actual; 
			<not(s)> actual;
			<pp-ql-string> actual => type
			