lexical syntax

  "#" [0-9A-F][0-9A-F][0-9A-F][0-9A-F][0-9A-F][0-9A-F] -> COLOR

templates // top-level
  
  QLS.QLS = <
  	<StyleDef*; separator="\n\n">
  
  	<Page*; separator="\n\n">
  >
  
  FormRef.FormRef = <<ID>>
  
	Page.Page = <
		page <STRING> for <FormRef+; separator=" "> {
			<PagePart*; separator="\n">
		}
	>

  PagePart = <<Ref>>
	PagePart = <<Section>>

templates // section

  Section.Section = <
  	section <STRING> {
  		<SectionPart*; separator="\n">
  	}
  >
  
  SectionPart = <<Ref>>
  SectionPart = <<SubSection>>
  
templates // subsection

  SubSection.SubSection = <
  	subsection <STRING> {
  		<SubSectionPart*; separator="\n">
  	}
  >
  
  SubSectionPart = <<Ref>>


templates // questions
	
	Ref.SimpleRef = <<ID>>
	
	Ref.StyledRef = <<ID> <Styles>>

	
templates // style definitions
	
	StyleDef.StyleDef = <
		define style <ID> {
			<DefStyle*;separator=" ">
		}
	>
	
	DefStyle = <<StyleComponent>>
	DefStyle = <<TypedWidgetComponent>>
		
	TypedWidgetComponent.TypedWidget = <<Type> <WidgetComponent>>

templates // types redefined

	Type.BoolTy = <boolean>
	Type.StringTy = <string>
	Type.IntTy = <integer>
	Type.DateTy = <date>
	Type.FloatTy = <decimal>
	Type.MoneyTy = <money>

templates // styling

  Styles.Styles = < { 
  		<Style*; separator=" ">
		}
	>
	
	Style = <<StyleComponent>>
	Style = <<WidgetComponent>>
	
	StyleComponent.StyleRef = <use <ID+;separator=",">>
  StyleComponent.Font   = <font = <STRING>>
  StyleComponent.Color  = <color = <COLOR>>
  
  WidgetComponent.Widget = <widget = <Widget>>
  
  Widget.CheckBox = <checkbox>
  Widget.Radio    = <radio>
  Widget.Dropdown = <dropdown>
  Widget.Text     = <text>
  Widget.Slider   = <slider>
  Widget.DatePicker = <datepicker>